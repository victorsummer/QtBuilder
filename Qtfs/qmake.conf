#
# qmake configuration for the Freescale iMX6 boards (single, dual and quad)
#

include(../common/linux_device_pre.conf)



# Change the required paths here
QMAKE_INCDIR          = $${ROOTFS_PATH}/usr/include
QMAKE_LIBDIR          = $${ROOTFS_PATH}/usr/lib

QMAKE_INCDIR_OPENGL_ES2 = $${ROOTFS_PATH}/usr/include/GLES2
QMAKE_LIBDIR_OPENGL_ES2 = $${ROOTFS_PATH}/usr/lib
QMAKE_INCDIR_EGL      = $${QMAKE_INCDIR_OPENGL_ES2}
QMAKE_LIBDIR_EGL      = $${QMAKE_LIBDIR_OPENGL_ES2}

QMAKE_LIBS             += -lz -lpng
QMAKE_LIBS_EGL         += -lEGL -Wl,--library-path=$${ROOTFS_PATH}/usr/lib,-rpath-link=$${ROOTFS_PATH}/usr/lib
QMAKE_LIBS_OPENGL_ES2  += -lGLESv2 -lEGL -lGAL -Wl,--library-path=$${ROOTFS_PATH}/usr/lib,-rpath-link=$${ROOTFS_PATH}/usr/lib
QMAKE_LIBS_OPENVG      += -lOpenVG -lEGL -lGAL

IMX6_CFLAGS             = -march=armv7-a -mfpu=neon -DLINUX=1 -DEGL_API_FB=1
QMAKE_CFLAGS           += $$IMX6_CFLAGS
QMAKE_CXXFLAGS         += $$IMX6_CFLAGS

DISTRO_OPTS = soft-float

# Preferred eglfs backend
EGLFS_DEVICE_INTEGRATION = eglfs_viv

include(../common/linux_arm_device_post.conf)

load(qt_config)
